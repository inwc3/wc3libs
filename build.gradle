plugins {
    id "java-library"
    id "antlr"
    id "jacoco"
    id "idea"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

// Project Info
group 'net.moonlightflower'
version '1.1.2'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    antlr "org.antlr:antlr4:4.9.2"
    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // Libs
    implementation group: 'com.github.inwc3', name: 'jmpq3', version: 'acf47d566f'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    implementation group: 'com.dorkbox', name: 'PeParser', version: '3.1'
    implementation group: 'com.dorkbox', name: 'PeParser-Dorkbox-Util', version: '2.7'
    implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-jpeg', version: '3.7.0'
    implementation 'com.jcraft:jzlib:1.1.3'

    // TestNG
    testImplementation 'org.testng:testng:6.14.3'
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

task cleanAntlrDir {
    delete "${buildDir}/generated-src/"
    delete fileTree("src/main/java/net/moonlightflower/wc3libs/antlr").include('*.*')
}

generateGrammarSource { doLast {
    println "Copying generated grammar lexer/parser files to main directory."

    copy {
        from "${buildDir}/generated-src/antlr/main"
        into "src/main/java/net/moonlightflower/wc3libs/antlr"
    }
    project.delete fileTree("${buildDir}/generated-src/antlr/main").include('*.*')
}}


generateGrammarSource.dependsOn cleanAntlrDir
compileJava.dependsOn generateGrammarSource
